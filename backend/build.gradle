buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:3.9.8")
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

apply plugin: 'base'
apply plugin: 'net.serenity-bdd.serenity-gradle-plugin'

serenity {
    reports = ["single-page-html"]
    requirementsBaseDir = "src/test/resources/features"
}

tasks.withType(Test) {
    systemProperty "serenity.report.encoding", "UTF-8"
    systemProperty "webdriver.timeouts.implicitlywait", "10000"
    systemProperty "serenity.take.screenshots", "FOR_FAILURES"
}


repositories {
    mavenCentral()
    gradlePluginPortal()
}

springBoot {
    mainClass = 'com.academy.BackendApplication'
}

group = 'com.academy'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
    options.encoding = 'UTF-8'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    // Security & JWT
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // BDD testing dependencies
    testImplementation 'io.cucumber:cucumber-java:7.14.0'
    testImplementation 'io.cucumber:cucumber-spring:7.14.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.14.0'
    testImplementation 'io.rest-assured:rest-assured:5.3.2'

    // Additional Rest-Assured modules for JSON/XML path assertions
    testImplementation 'io.rest-assured:json-path:5.3.2'
    testImplementation 'io.rest-assured:xml-path:5.3.2'

    // JUnit Platform Suite
    testImplementation 'org.junit.platform:junit-platform-suite:1.10.1'
    testImplementation 'org.junit.platform:junit-platform-suite-api:1.10.1'
    testImplementation 'org.junit.platform:junit-platform-suite-engine:1.10.1'

    // Serenity BDD (API reporting)
    testImplementation 'net.serenity-bdd:serenity-core:3.9.8'
    testImplementation 'net.serenity-bdd:serenity-junit5:3.9.8'
    testImplementation 'net.serenity-bdd:serenity-rest-assured:3.9.8'
    testImplementation 'net.serenity-bdd:serenity-cucumber:3.9.8'
    // Screenplay support for REST interactions
    testImplementation 'net.serenity-bdd:serenity-screenplay:3.9.8'
    testImplementation 'net.serenity-bdd:serenity-screenplay-rest:3.9.8'
    testImplementation 'org.json:json:20231013'

    // Matchers for assertions
    testImplementation 'org.hamcrest:hamcrest:2.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // Add JUnit 4 so JUnit4-style runners (CucumberWithSerenity) can compile
    testImplementation 'junit:junit:4.13.2'

    // Allow JUnit4 tests to be discovered and run on the JUnit Platform
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.9.3'
}

test {
    useJUnitPlatform()
    systemProperties = [
        'serenity.project.name': 'Academy Backend BDD Tests',
        'serenity.console.colors': 'true',
        'serenity.logging': 'VERBOSE',
        'serenity.reports.show.step.details': 'true',
        'serenity.test.root': 'src/test/resources/features',
        'cucumber.publish.quiet': 'true',
            'cucumber.filter.tags': System.getProperty('cucumber.filter.tags', ''),
            // Prefer the Spring object factory when multiple factories are on the classpath
            'cucumber.object-factory': 'io.cucumber.spring.SpringFactory'
    ]
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat 'full'
        showStandardStreams = true
    }
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    finalizedBy 'aggregate'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

tasks.withType(Test) {
    // Ensure Serenity writes outcome files into the build/serenity folder which the
    // aggregator reads. Previously this used project.reporting.baseDir (build/reports)
    // which caused the aggregator to find 0 tests.
    systemProperty "serenity.outputDirectory", "${project.buildDir}/serenity"
    systemProperty "serenity.project.name", "Academy Backend BDD Tests"
    finalizedBy(tasks.aggregate)
}
